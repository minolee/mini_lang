PROGRAM : [ID_DECLARATION]?[SENTENCES]

SENTENCES : [LEGAL_SENTENCE][SENTENCES]?
LEGAL_SENTENCE : [FUNCTION_DECLARATION]|[COMPOUND_EXPR]

FUNCTION_DECLARATION : <FUNCTION><ID><LPARENTHESE>[ID_LIST]<RPARENTHESE>[COMPOUND_EXPR]

ID_DECLARATION : <DECLARATION><COLON>[ID_LIST]
ID_LIST : <ID>[ID_LIST_]?
ID_LIST_ : <COMMA><ID>[ID_LIST_]?

COMPOUND_EXPRS : [COMPOUND_EXPR][COMPOUND_EXPRS]?

COMPOUND_EXPR : [DO_EXPR]|[IF_EXPR]|[CONCURRENT_EXPR]|[ASSIGN_EXPR]|[SKIP_EXPR]|[ABORT_EXPR]|[PRINT_EXPR]|<LBRACE>[ID_DECLARATION]?[COMPOUND_EXPRS]<RBRACE>|[RETURN_EXPR]

SKIP_EXPR : <_SKIP>
ABORT_EXPR : <ABORT>
IF_EXPR : <IF>[CASE_LIST]?<END_IF>
DO_EXPR : <DO>[CASE_LIST]?<END_DO>

CASE_LIST : [CASE][CASE_LIST_]?
CASE_LIST_ : <SEP>[CASE][CASE_LIST_]?
CASE : [EXPR]<THEN>[COMPOUND_EXPR]

PRINT_EXPR : <PRINT><LPARENTHESE>[EXPR]<RPARENTHESE>

EXPR_LIST : [EXPR][EXPR_LIST_]?
EXPR_LIST_ : <COMMA>[EXPR][EXPR_LIST_]?


EXPR : [EXPR1]|[EXPR1]((<EQ>|<NE>)[EXPR])
EXPR1 : [EXPR2]|[EXPR2]((<GT>|<LT>|<GTE>|<LTE>)[EXPR1])
EXPR2 : [EXPR3]|[EXPR3]((<PLUS>|<MINUS>)[EXPR2])
EXPR3 : [EXPR4]|[EXPR4]((<MULTIPLY>|<DIVIDE>|<REMAINDER>)[EXPR3])
EXPR4 : <MINUS>?[BASE_CASE]

BASE_CASE : <LPARENTHESE>[EXPR]<RPARENTHESE>|<ID>|<NUMBER>|[INVOKE_EXPR]

INVOKE_EXPR : <ID><LPARENTHESE>[EXPR_LIST]?<RPARENTHESE>

CONCURRENT_EXPR : <ID>[CONCURRENT_EXPR_][EXPR]
CONCURRENT_EXPR_ : (<COMMA><ID>[CONCURRENT_EXPR_][EXPR]<COMMA>)|<ASSIGN>

RETURN_EXPR : <RETURN>[EXPR]

ASSIGN_EXPR : <ID><ASSIGN>[EXPR]